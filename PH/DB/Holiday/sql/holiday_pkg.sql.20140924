CREATE OR REPLACE PACKAGE holiday_pkg
IS
    FUNCTION is_holiday_by_country(
        in_posting_date    IN OL_TXN_HEADER.OTH_HOST_POSTING_DATE%TYPE,
        in_country         IN HOLIDAY.H_COUNTRY%TYPE
    ) RETURN CHAR;

END holiday_pkg;
/

CREATE OR REPLACE PACKAGE BODY holiday_pkg
IS

    ------------------------------------
    FUNCTION is_holiday_by_country (
        in_posting_date    IN OL_TXN_HEADER.OTH_HOST_POSTING_DATE%TYPE,
        in_country         IN HOLIDAY.H_COUNTRY%TYPE
    ) RETURN CHAR
    IS

        iDOW         INTEGER := 0;
        iHolidayCnt  INTEGER := 0;

        vRtn         CHAR(1) := 'N';

    BEGIN

        SELECT count(1)
        INTO iHolidayCnt
        FROM holiday
        WHERE h_country = in_country
        AND h_date = in_posting_date
        AND h_is_non_holiday = 0; -- 0 means holiday

        if iHolidayCnt > 0 then
            vRtn := 'Y';
        else
            -- for Database NSL_DATE_LANGUAGE set AMERICAN, 1: Sun; ... 7: Sat
            -- reset 0 - 6, 0:Sun,... 6: Sat
            SELECT TO_NUMBER (TO_CHAR (TO_DATE (in_posting_date, 'YYYYMMDD'), 'D')) - 1
            INTO iDOW
            FROM dual;

            if iDOW in (0, 6) then
                SELECT count(1)
                INTO iHolidayCnt
                FROM holiday
                WHERE h_country = in_country
                AND h_date = in_posting_date
                AND h_is_non_holiday = 1; -- 1 means workding_day

                if (iHolidayCnt > 0) then
                    vRtn := 'N';
                else
                    vRtn := 'Y';
                end if;

            end if;
        end if;

        return vRtn;
    END is_holiday_by_country;

END holiday_pkg;
/

