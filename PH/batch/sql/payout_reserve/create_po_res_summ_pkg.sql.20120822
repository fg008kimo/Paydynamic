CREATE OR REPLACE PACKAGE PO_RES_SUMM_PKG is

	TYPE   PO_RES_SUMM_type is REF cursor return PO_RES_SUMM_VIEW%rowtype;

	function F_po_res_summ (in_amt_type	merchant_reserved_amt.mr_type%type) 
	return po_res_summ_tab;

end PO_RES_SUMM_PKG;
/

CREATE OR REPLACE PACKAGE BODY PO_RES_SUMM_PKG
IS
   FUNCTION F_po_res_summ (in_amt_type    merchant_reserved_amt.mr_type%type) 
	RETURN po_res_summ_tab
   IS
      Result     po_res_summ_tab := po_res_summ_tab();
      n	         INTEGER := 0;

      v_client_id         clients.client_id%type;
      v_client_name       clients.client_name%type;
      v_merchant_id       merchant_reserved_amt.mr_merchant_id%type;
      v_merchant_name     merch_detail.name%type;
      v_country_id        merchant_reserved_amt.mr_country_id%type;
      v_country_name      country.country_name%type;
      v_service_code      merchant_reserved_amt.mr_service_code%type;
      v_service_name      def_service_code.sc_desc%type;
      v_currency_id       merchant_reserved_amt.mr_currency_id%type;
      v_status            merchant_reserved_amt.mr_status%type; 

      v_tmp_res_amt       merchant_reserved_amt.mr_reserved_amount%type;
      v_tmp_effect_date   merchant_reserved_amt.mr_effect_date%type;

      v_sun_res_amt       merchant_reserved_amt.mr_reserved_amount%type;
      v_sun_effect_date   merchant_reserved_amt.mr_effect_date%type;
      v_mon_res_amt       merchant_reserved_amt.mr_reserved_amount%type;
      v_mon_effect_date   merchant_reserved_amt.mr_effect_date%type;
      v_tue_res_amt       merchant_reserved_amt.mr_reserved_amount%type;
      v_tue_effect_date   merchant_reserved_amt.mr_effect_date%type;
      v_wed_res_amt       merchant_reserved_amt.mr_reserved_amount%type;
      v_wed_effect_date   merchant_reserved_amt.mr_effect_date%type;
      v_thu_res_amt       merchant_reserved_amt.mr_reserved_amount%type;
      v_thu_effect_date   merchant_reserved_amt.mr_effect_date%type;
      v_fri_res_amt       merchant_reserved_amt.mr_reserved_amount%type;
      v_fri_effect_date   merchant_reserved_amt.mr_effect_date%type;
      v_sat_res_amt       merchant_reserved_amt.mr_reserved_amount%type;
      v_sat_effect_date   merchant_reserved_amt.mr_effect_date%type;

      cursor     cur_merchant_account is
          select clients.client_id as client_id, client_name, 
                 mr_merchant_id, name , 
                 mr_country_id, country_name, 
                 mr_service_code, sc_desc, 
                 mr_currency_id, 
                 mr_status
            from clients, def_service_code, country, merch_detail, merchant_reserved_amt
           where mr_type = in_amt_type
             and mr_merchant_id = merchant_id 
             and mr_country_id = country_code
             and mr_service_code = sc_code
             and merch_detail.client_id = clients.client_id
        group by clients.client_id, client_name,
                 mr_merchant_id, name, 
                 mr_country_id, country_name, 
                 mr_service_code, sc_desc, 
                 mr_currency_id, 
                 mr_status
        order by clients.client_id, client_name,
                 mr_merchant_id, name,
                 mr_country_id, country_name,
                 mr_service_code, sc_desc,
                 mr_currency_id,
                 mr_status;
   BEGIN
      OPEN cur_merchant_account;

      LOOP
         FETCH cur_merchant_account
         INTO  v_client_id,
               v_client_name,
               v_merchant_id,
               v_merchant_name,
               v_country_id,
               v_country_name,
               v_service_code,
               v_service_name,
               v_currency_id,
               v_status; 

         EXIT WHEN cur_merchant_account%NOTFOUND;

         for v_day_of_week in 0..6 loop
             v_tmp_res_amt     := NULL;
             v_tmp_effect_date := NULL;

             begin
                 select mr_reserved_amount, mr_effect_date
                   into v_tmp_res_amt, v_tmp_effect_date
                   from merchant_reserved_amt
                  where mr_merchant_id = v_merchant_id
                    and mr_currency_id = v_currency_id
                    and mr_country_id  = v_country_id
                    and mr_service_code = v_service_code
                    and mr_type = in_amt_type
                    and mr_status = v_status
                    and mr_day_of_week = v_day_of_week
                    and mr_effect_date = (select max(mr_effect_date)
                                            from merchant_reserved_amt
                                           where mr_merchant_id = v_merchant_id
                                             and mr_currency_id = v_currency_id
                                             and mr_country_id  = v_country_id
                                             and mr_service_code = v_service_code
	                                     and mr_type = in_amt_type
                                             and mr_status = v_status
                                             and mr_day_of_week = v_day_of_week
                                         );
             exception
                 when no_data_found then
	             v_tmp_res_amt := NULL;
                     v_tmp_effect_date := NULL;
	     end;

             if (v_day_of_week = 0) then 
                 v_sun_res_amt       := v_tmp_res_amt;
                 v_sun_effect_date   := v_tmp_effect_date;
             elsif (v_day_of_week = 1) then
                 v_mon_res_amt       := v_tmp_res_amt;
                 v_mon_effect_date   := v_tmp_effect_date;
             elsif (v_day_of_week = 2) then
                 v_tue_res_amt       := v_tmp_res_amt;
                 v_tue_effect_date   := v_tmp_effect_date;
             elsif (v_day_of_week = 3) then
                 v_wed_res_amt       := v_tmp_res_amt;
                 v_wed_effect_date   := v_tmp_effect_date;
             elsif (v_day_of_week = 4) then
                 v_thu_res_amt       := v_tmp_res_amt;
                 v_thu_effect_date   := v_tmp_effect_date;
             elsif (v_day_of_week = 5) then
                 v_fri_res_amt       := v_tmp_res_amt;
                 v_fri_effect_date   := v_tmp_effect_date; 
             elsif (v_day_of_week = 6) then
                 v_sat_res_amt       := v_tmp_res_amt;
                 v_sat_effect_date   := v_tmp_effect_date;
             end if;
         end loop;

         Result.EXTEND;
         n := n+1;

         result (n) :=
            PO_RES_SUMM_OBJ(v_client_id,
                            v_client_name,
                            v_merchant_id,
                            v_merchant_name,
                            v_country_id,
                            v_country_name,
                            v_service_code,
                            v_service_name,
                            v_currency_id,
                            v_status,
                            v_sun_res_amt,
                            to_char(v_sun_effect_date, 'YYYYMMDDHH24MISS'),
                            v_mon_res_amt,
                            to_char(v_mon_effect_date, 'YYYYMMDDHH24MISS'),
                            v_tue_res_amt,
                            to_char(v_tue_effect_date, 'YYYYMMDDHH24MISS'),
                            v_wed_res_amt,
                            to_char(v_wed_effect_date, 'YYYYMMDDHH24MISS'),
                            v_thu_res_amt,
                            to_char(v_thu_effect_date, 'YYYYMMDDHH24MISS'),
                            v_fri_res_amt,
                            to_char(v_fri_effect_date, 'YYYYMMDDHH24MISS'),
                            v_sat_res_amt,
                            to_char(v_sat_effect_date, 'YYYYMMDDHH24MISS')
			   );
      END LOOP;

      CLOSE cur_merchant_account; 

      RETURN (Result);
   END;

END PO_RES_SUMM_PKG;
/
